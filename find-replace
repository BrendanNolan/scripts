#! /usr/bin/env fish

function find_replace
    argparse 'h/help' 'f/find=' 'r/replace=' 'p/path=' 'regex' -- $argv

    if set -q _flag_help
        echo 'Usage:'
        echo '    find_replace -f <search text> -r <replacement text> -p <path> [--regex]'
        exit 0
    end

    set -l path .
    if set -q _flag_path
        set path $_flag_path
    end

    set -l to_find (string escape --style=regex $_flag_find)
    if set -q _flag_regex
        set to_find $_flag_find
    end

    set -l files (rg -l $to_find $path)
    set -l sed_search "s@$to_find@$_flag_replace@g"

    switch (uname)
        case Darwin # macOS
            echo $files | xargs gsed -i $sed_search
        case Linux
            echo $files | xargs sed -i $sed_search
        case '*'
            echo 'You seem to be neither on macOS nor on Linux. Exiting.'
    end
end

find_replace $argv
